note
	description: "Main window for this application"
	author: "Generated by the New Vision2 Application Wizard."
	date: "$Date: 2016/3/17 18:26:37 $"
	revision: "1.0.0"

class
	MAIN_WINDOW

inherit
	EV_TITLED_WINDOW
		redefine
			create_interface_objects,
			initialize,
			is_in_default_state
		end

	INTERFACE_NAMES
		export
			{NONE} all
		undefine
			default_create, copy
		end

	OBSERVER
		undefine
			default_create, copy
		redefine
			notify
		end

create
	default_create

feature {NONE} -- General Configuration Variables

	floors: INTEGER
	tickrate: DOUBLE
	up_buttons: ARRAY[EL_BUTTON]
	down_buttons: ARRAY[EL_BUTTON]
	dest_buttons: ARRAY[EL_BUTTON]

feature {NONE} -- Initialization

	create_interface_objects
			-- <Precursor>
		do
				-- Create main container.
			create main_container
				-- Create floor display
			create status_panel
				-- Create elevator interior org box
			create elevator_interior_box
				-- Create building display org box
			create building_box
				-- Create scrollpanes
			create building_scroll_area
			create elevator_interior_scroll_area

				--create arrays
				--building floors
			create building.make_empty
				--destination buttons
			create dest_buttons.make_empty
				--up call buttons
			create up_buttons.make_empty
				--down call buttons
			create down_buttons.make_empty
			floors := 0
			tickrate := 0
			create model.make(2)
			create thread

		end

	initialize
			-- Build the interface for this window.
		local
			error_dialog: EV_INFORMATION_DIALOG
		do
			Precursor {EV_TITLED_WINDOW}

			from
			until floors >= 2
			loop
				floors := get_num_floors
				if floors < 2 then
					create error_dialog.make_with_text ("Invalid number of floors.%NPlease choose an integer number%Ngreater than 2")
					error_dialog.show_modal_to_window (Current)
				end
			end
			from
			until tickrate >= 0.1
			loop
				tickrate := get_tick_rate
				if tickrate < 0.1 then
					create error_dialog.make_with_text ("Invalid tick rate.%NPlease choose a floating point%Nnumber greater than 0.1")
					error_dialog.show_modal_to_window (Current)
				end
			end

			create model.make (floors)

			build_building_panel
			build_elevator_interior
			build_main_container
			extend (main_container)

				-- Execute `request_close_window' when the user clicks
				-- on the cross in the title bar.
			close_request_actions.extend (agent request_close_window)

				-- Set the title of the window.
			set_title (Window_title)

				-- Set the initial size of the window.
			set_size (Window_width, Window_height)

			set_up_model_and_controller
		end

	is_in_default_state: BOOLEAN
			-- Is the window in its default state?
			-- (as stated in `initialize')
		do
			Result := (width = Window_width) and then
				(height = Window_height) and then
				(title.is_equal (Window_title))
		end

feature {NONE} -- Implementation, Close event

	request_close_window
			-- Process user request to close the window.
		local
			question_dialog: EV_CONFIRMATION_DIALOG
		do
			create question_dialog.make_with_text (Label_confirm_close_window)
			question_dialog.show_modal_to_window (Current)

			if question_dialog.selected_button ~ (create {EV_DIALOG_CONSTANTS}).ev_ok then
					-- Destroy the window.
				destroy

					-- End the application.
					--| TODO: Remove next instruction if you don't want the application
					--|       to end when the first window is closed..
				if attached (create {EV_ENVIRONMENT}).application as a then
					a.destroy
				end
			end
		end

feature {NONE} -- Implementation, Get Floors

	get_num_floors: INTEGER
		-- Process user request to close the window.
	local
		input_dialog: INPUT_DIALOG
	do
		Result := 0
		create input_dialog
		input_dialog.show_modal_to_window (Current)
		if input_dialog.output.is_integer then
			Result := input_dialog.output.to_integer
		end
	end

feature {NONE} -- Implementation, Get tickrate

	get_tick_rate: DOUBLE
	local
		input_dialog: INPUT_DIALOG
	do
		Result := 0
		create input_dialog
		input_dialog.label.set_text("Please input the desired tick%Nrate (in seconds)")
		input_dialog.show_modal_to_window (Current)
		if input_dialog.output.is_double then
			Result := input_dialog.output.to_double
		end
	end

feature {NONE} -- Building Panel Implementation

	building_box: EV_VERTICAL_BOX
	building: ARRAY[BUILDING_FLOOR]

	build_building_panel
	local
		i: INTEGER;
		floor: BUILDING_FLOOR
	do

		from
			i := 0
		until
			i = floors
		loop
			create floor.default_create
			floor.set_floor(i + 1)
			floor.up.select_actions.extend (agent click_up_button(i))
			up_buttons.force(floor.up, i)
			floor.down.select_actions.extend (agent click_down_button(i))
			down_buttons.force(floor.down, i)
			building.force (floor, i)
			i := i + 1
		end
		from
			i := building.upper
		until
			i < building.lower
		loop
			building_box.extend (building[i])
			i := i - 1
		end
	end

feature {NONE} -- Elevator Interior Implementation

	elevator_interior_box: EV_VERTICAL_BOX
	status_panel: EV_LABEL

	build_elevator_interior
	local
		i, j: INTEGER
		button: EL_BUTTON
		button_row: EV_HORIZONTAL_BOX
		dest_buttons_box: EV_VERTICAL_BOX
	do
		from
			i := 0
		until
			i = floors
		loop
			create button
			button.set_text((i + 1).out)
			button.select_actions.extend (agent click_dest_button(i))
			button.set_minimum_size (50, 50)
			dest_buttons.force (button, i)
			i := i + 1
		end
		create dest_buttons_box
		dest_buttons_box.set_padding_width (10)
		from
			i := dest_buttons.lower
		until
			i > dest_buttons.upper
		loop
			create button_row
			button_row.set_padding_width (10)
			from
				j := 0
			until
				j = 3 OR i > dest_buttons.upper
			loop
				button_row.extend (dest_buttons[i])
				j := j + 1
				i := i + 1
			end
			dest_buttons_box.put_front (button_row)
		end
		status_panel.set_text ("R 0")
		status_panel.set_foreground_color(create {EV_COLOR}.make_with_8_bit_rgb (0, 255, 0))
		status_panel.set_background_color(create {EV_COLOR}.make_with_8_bit_rgb (0,0,0))
		status_panel.set_font (create {EV_FONT}.make_with_values ({EV_FONT_CONSTANTS}.family_typewriter,
																  {EV_FONT_CONSTANTS}.weight_bold,
																  {EV_FONT_CONSTANTS}.shape_regular,
																  36))
		elevator_interior_box.extend (status_panel)
		elevator_interior_box.extend (dest_buttons_box)

	end

	click_up_button(floor: INTEGER)
	require
		valid_floor: building.upper >= floor AND building.lower <= floor
	do
		up_buttons[floor].activate
		--status_panel.set_text ("up " + (floor+1).out)
	end

	click_down_button(floor: INTEGER)
	require
		valid_floor: building.upper >= floor AND building.lower <= floor
	do
		down_buttons[floor].activate
		--status_panel.set_text ("down " + (floor+1).out)
	end

	click_dest_button(floor: INTEGER)
	require
		valid_floor: building.upper >= floor AND building.lower <= floor
	do
		dest_buttons[floor].activate
		--status_panel.set_text ("dest " + (floor+1).out)
	end

feature {NONE} -- Thread Implementation

	model: ELEVATOR_MODEL
	thread: ELEVATOR_CONTROL

	set_up_model_and_controller
	do
		model.add_observer (Current)
		thread.set_tickrate (tickrate)
		thread.attach_destination_buttons (dest_buttons)
		thread.attach_down_buttons (down_buttons)
		thread.attach_up_buttons (up_buttons)
		thread.attach_model (model)
		thread.ready_gui
		thread.model_ok
		if thread.gui_attached and thread.model_valid then
			thread.launch
		else
			status_panel.set_text ("FAIL")
		end
	end

feature {NONE} -- Implementation

	main_container: EV_HORIZONTAL_BOX
			-- Main container (contains all widgets displayed in this window).
	building_scroll_area: EV_SCROLLABLE_AREA
			-- Scrollable area for holding building representation
	elevator_interior_scroll_area: EV_SCROLLABLE_AREA
			-- Scrollable area for holding elevator interior representation



	build_main_container
			-- Populate `main_container'.
		do
			building_scroll_area.set_minimum_size (175, 600)
			building_scroll_area.extend (building_box)
			elevator_interior_scroll_area.set_minimum_size (175, 600)
			elevator_interior_scroll_area.extend (elevator_interior_box)
			main_container.extend (building_scroll_area)
			main_container.extend (elevator_interior_scroll_area)
		ensure
			main_container_created: main_container /= Void
		end

feature {NONE} -- observer pattern

	notify
	local s: STRING; i: INTEGER
	do
		s := ""
		if model.mode = model.mode_ascending then
			s := "U "
		elseif model.mode = model.mode_descending then
			s := "D "
		elseif model.mode = model.mode_resting then
			s := "R "
		end
		s := s + (model.floor+1).out
		status_panel.set_text (s)
		from i := building.lower
		until i > building.upper
		loop
			if model.floor = i then
				if model.doors_open then
					building[i].elevator_opened_doors
				else
					building[i].elevator_entered
				end
			else
				building[i].elevator_left
			end
			i := i + 1
		end
	end

feature {NONE} -- Implementation / Constants

	Window_title: STRING = "Simulation"
			-- Title of the window.

	Window_width: INTEGER = 375
			-- Initial width for this window.

	Window_height: INTEGER = 800
			-- Initial height for this window.

end
